buildscript {
  repositories { 
    mavenLocal() 
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
  }
}

plugins { 
  id "io.spring.dependency-management" version "1.0.4.RELEASE" 
  id "com.github.kt3k.coveralls" version "2.8.1" 
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

description = 'Tools for extracting metadata from OCR-D bagIt container/workspace.'
group = 'edu.kit.ocrd'
version = '0.4.0'

println "Building ${name} version: ${version}"

jar {
  enabled = true
}
bootJar {
  enabled = false
}

repositories { 
  mavenLocal() 
  mavenCentral()
  jcenter()
  maven {
    url "./localRepo"
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty('travis')) {
  println 'Using travis profile for building ' + project.getName()
  apply from: 'gradle/build_dependencies.gradle'
  apply from: 'gradle/profile-travis.gradle'
} 

dependencies {
  // Local dependencies
  implementation "org.fzk.ipe:Tools:1.6"
  implementation "jaxen:jaxen:1.1.6"
  implementation "org.jdom:jdom:1.1"
  //  implementation "gov.loc:bagit:5.2.0"
  // sub modules
  implementation "edu.kit.ocrd:ocrd-workspace:0.2.0"
  // bag it
  implementation "edu.kit.ocrd:bagging:0.2.0"
    
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "commons-io:commons-io:2.5"
  
  
  // ArangoDB
  implementation "com.arangodb:arangodb-spring-data:3.1.1"
   
  implementation "org.slf4j:slf4j-api:1.7.25"
  implementation "ch.qos.logback:logback-classic:1.2.3"
  

  testImplementation "junit:junit:4.12"

}

jacocoTestReport {
  group = "Reporting"
  reports {
    xml.enabled true
    html.enabled true
    html.destination file("${buildDir}/reports/coverage")
  }
  afterEvaluate {
    classDirectories.from(classDirectories.files.collect {
        fileTree(dir: it)
      })
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'
  options.deprecation = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
